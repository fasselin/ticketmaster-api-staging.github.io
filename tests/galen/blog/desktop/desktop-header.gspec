##
#galen check tests/galen/blog/desktop/desktop-header.gspec --url http://ticketmaster-api-staging.github.io/blogs --size 1469x1024 --htmlreport tests/galen/reports/blog/desktop-header
##

@set
    white "rgba(255, 255, 255, 1)"
    whiteDisabled "rgba(255, 255, 255, 0.7)"

@objects
    body-wrapper                css  .top-bar
    menu-wrapper                css  div.body-wrapper
    header-menu                 css  #menu
    logo                        css  #header-logo img
    search-button               css  #search
    nav-item-*                  css  #menu a

= Blogs page desktop header (desktop-header.gspec) =
        #is12?
        global:
            count any nav-item-* is 6 to 12

        logo:
            visible
            width > 10 % of screen/width
            inside menu-wrapper 10 px left, 20 to 30px top
            near header-menu ~ 42 px left

        search-button:
            visible
            inside menu-wrapper ~88 px right, 20 to 25px top

        header-menu:
            visible

= I see the TM logo, the header menu and the search button in one row=
        logo:
            aligned horizontally centered  header-menu 3px
            aligned horizontally centered  search-button 3px


= I see such menu items in the header menu =
        nav-item-1:
            % text is "PRODUCTS & DOCS"
            aligned horizontally all nav-item-2
            css color is ${whiteDisabled}
        nav-item-2:
            % text is "PARTNERS"
            aligned horizontally all nav-item-3
            css color is ${whiteDisabled}
        nav-item-3:
            % text is "SUPPORT"
            aligned horizontally all nav-item-4
            css color is ${whiteDisabled}
        nav-item-4:
            % text is "BLOGS"
            aligned horizontally all nav-item-5
            css color is ${white}
        nav-item-5:
            % text is "EVENTS"
            aligned horizontally all nav-item-6
            css color is ${whiteDisabled}
        nav-item-6:
            % text is "OPEN SOURCE"
            css color is ${whiteDisabled}